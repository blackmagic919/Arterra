#pragma kernel March

#include "Assets/Resources/Compute/Utility/GetIndex.hlsl"
#include "Assets/Resources/Compute/TerrainGeneration/BaseGeneration/MarchingTables.hlsl"

static const float3 Epsilon = float3(1E-6, 1E-6, 1E-6);

struct vertex{
    //Calculate vertex normals in vertex shader cause it's too hard to get here
    //Don't use smooth normals because out of bounds are unresolvable
    float3 tri; 
    int2 material; //x = base, y = water
};

struct CubeCorner{
    float density[2];
    int material;
};

struct MarchCube{
    CubeCorner corners[8];
};

struct pInfo{
    uint axis[3];
};


const static int numThreads = 8;



/*               
*  _____________  8  ------
* |  _________  | 7 6      |<-NumOfPoints, Density Map Size
* | |_|_|_|_|_| | 6 5 5 -  |
* | |_|_|_|_|_| | 5 4 4  | |
* | |_|_|_|_|_| | 4 3 3  |<+--ChunkSize / meshSkipInc
* | |_|_|_|_|_| | 3 2 2  | |  "Real Vertices"
* | |_|_|_|_|_| | 2 1 1 _| |
* |_____________| 1_^_^____|
*Purpose:           ^ | |
*   Border Normals⅃ | |
*          Last Edge⅃ |     
*      Number of Cubes⅃
*
*    7---6----6
*   7|       5|
*  / 11      /10
* 4--+-4---5  |
* |  |     9  |
* 8  3---2-+--2
* | 3      | 1
* 0----0---1/
* 
* z
* ^     .--------.
* |    /|       /|
* |   / |      / |    y
* |  .--+-----.  |   /\
* |  |  |     |  |   /
* |  |  .-----+--.  /
* |  | /      | /  /
* | xyz_______./  /
* +---------> x  /
*/

//Map Info
StructuredBuffer<uint> MapInfo;

const static int POINT_STRIDE_4BYTE = 1;
float IsoLevel;
uint3 GridSize;

//Output
RWStructuredBuffer<uint> counter;
uint3 counterInd; //x -> vertex, y -> baseTri, z -> waterTri

RWStructuredBuffer<vertex> vertexes;

RWStructuredBuffer<pInfo> triangles;
RWStructuredBuffer<uint> triangleDict;
uint bSTART_dict;
uint bSTART_verts;
uint bSTART_baseT;
uint bSTART_waterT;



float interpolate(float p1Val, float p2Val) {
    //If p1Val != p2Val, Epsilon is lost through float conversion, otherwise Epsilon prevents Nan
    return ((IsoLevel - p1Val) / (p2Val - p1Val + Epsilon.x)); 
}

MarchCube ReadCube(int3 coord){
    MarchCube cube; 

    [unroll]for(uint i = 0; i < 8; i++){
        CubeCorner corner;

        int rIndex = indexFromCoordIrregular(coord + positionFromIndex[i], GridSize.yz);
        uint mapData = MapInfo[rIndex];
        float density = ((mapData & 0xFF) / 255.0f);
        float viscosity = ((mapData >> 8) & 0xFF) / 255.0f;

        corner.density[0] = viscosity;
        corner.density[1] = density - viscosity;
        corner.material = ((mapData  >> 16) & 0x7FFF);
        cube.corners[i] = corner;
    }
    
    return cube;
}

void AddVertices(int3 coord, int caseIndex, uint IsWater, in MarchCube cube){
    uint cellClass = CellClass(caseIndex);
    uint count = VertexCount(cellClass); 
    for (uint i = 0; i < count; i++) {
        //Get the point indexes
        int edgeIndex = Vertices(caseIndex, i);
        int3 pInd = cornerIndexFromEdge[edgeIndex];

        if(pInd.x != 0) continue; //Duplicate Vertex

        //Figure out point positions
        int3 p1 = coord + positionFromIndex[pInd.x];
        int3 p2 = coord + positionFromIndex[pInd.y];

        if((uint)p2.x >= GridSize.x || (uint)p2.y >= GridSize.y || (uint)p2.z >= GridSize.z) 
            continue; //Out of Bounds

        vertex newVert;
        //Get point densities
        float p1Val = cube.corners[pInd.x].density[IsWater];
        float p2Val = cube.corners[pInd.y].density[IsWater];
        float interpFactor = interpolate(p1Val, p2Val);

        newVert.tri = (p1 + interpFactor * (p2 - p1));
        if((p1Val > p2Val) ^ IsWater) newVert.material = uint2(cube.corners[pInd.x].material, cube.corners[pInd.y].material);
        else newVert.material = uint2(cube.corners[pInd.y].material, cube.corners[pInd.x].material);

        //Append Vertex
        int appendInd = 0;
        InterlockedAdd(counter[counterInd.x], 1, appendInd);
        vertexes[bSTART_verts + appendInd] = newVert;

        //Set To Dictionary
        triangleDict[3 * indexFromCoordIrregular(coord, GridSize.yz) + pInd.z + bSTART_dict] = appendInd;
    }
}

void AddTriangulation(int caseIndex, int3 coord, int2 info){
    uint CClass = CellClass(caseIndex); 
    uint count = TriangleCount(CClass); 
    for (uint i = 0; i < count; i++) {
        pInfo baseTri;
        [unroll] for(uint v = 0; v < 3; v++){ //Get the dict index locations of the vertices
            uint vertexIndex = Triangulation(CClass, 3*i + v);
            int3 cornerInfo = cornerIndexFromEdge[Vertices(caseIndex, vertexIndex)];
            baseTri.axis[v] = 3 * indexFromCoordIrregular(coord + positionFromIndex[cornerInfo.x], GridSize.yz) + cornerInfo.z;
        }

        //Append Base Triangle
        int appendInd = 0;
        InterlockedAdd(counter[info.y], 1, appendInd);
        triangles[info.x + appendInd] = baseTri;
    }
}

[numthreads(numThreads,numThreads,numThreads)]
void March (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= GridSize.x || id.y >= GridSize.y || id.z >= GridSize.z)
        return;

    int3 oCoord = int3(id.xyz);
    int oIndex = indexFromCoordIrregular(id, GridSize.yz);
    MarchCube cube = ReadCube(oCoord);

    uint baseIndex = 0; uint waterIndex = 0;
    [unroll] for(uint u = 0; u < 8; u++){
        if (cube.corners[u].density[0] >= IsoLevel) baseIndex |= (1 << u);
        if (cube.corners[u].density[1] >= IsoLevel) waterIndex |= (1 << u);
    }
    
    AddVertices(oCoord, waterIndex, 1, cube);
    AddVertices(oCoord, baseIndex, 0, cube);

    //Only add triangles if a valid cube
    if(id.x >= (GridSize.x - 1) || id.y >= (GridSize.y - 1) || id.z >= (GridSize.z - 1)) 
        return;
    
    AddTriangulation(baseIndex, oCoord, int2(bSTART_baseT, counterInd.y));
    AddTriangulation(waterIndex, oCoord, int2(bSTART_waterT, counterInd.z));
}