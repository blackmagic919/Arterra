#pragma kernel Bake
#pragma multi_compile _ _MAIN_LIGHT_SHADOWS _MAIN_LIGHT_SHADOWS_CASCADE
#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"

#include "Assets/Resources/Compute/Utility/GetIndex.hlsl"
#include "Assets/Resources/Compute/MapData/WSDensitySampler.hlsl"

struct ScatterData{
    float3 inScatteredLight;
    float3 extinction;
};

RWStructuredBuffer<ScatterData> mapData;
RWStructuredBuffer<uint> treeLocks;
StructuredBuffer<float3> rayInfo;
uint screenHeight;
uint screenWidth;

float3 _LightDirection; //Global Variable

float _AtmosphereRadius;
uint _NumInScatterPoints;
uint _NumOpticalDepthPoints;
float _IsoLevel;

float2 raySphere(float3 s0, float sr, float3 r0, float3 rd) {
    float a = dot(rd, rd);
    float3 s0_r0 = r0 - s0;
    float b = 2.0 * dot(rd, s0_r0);
    float c = dot(s0_r0, s0_r0) - (sr * sr);
	float disc = b * b - 4.0 * a* c;
                    
    if (disc < 0.0) {
        return float2(-1.0, -1.0);
    }else{
        float t1 = max((-b - sqrt(disc)) / (2.0 * a), 0);
        float t2 = max((-b + sqrt(disc)) / (2.0 * a), 0);
        return float2(t1, t2-t1);
	}
}

float calculateOcclusionFactor(float3 rayOrigin, float3 rayDir, float rayLength){
    half cascadeIndex = ComputeCascadeIndex(rayOrigin);
    float stepSize = pow(2, cascadeIndex);
    int NumShadowPoints = max(1, rayLength / stepSize);

    float3 shadowPoint = rayOrigin;
    float transmittanceCount = 0;

    for(int i = 0; i < NumShadowPoints; i++){
        transmittanceCount += MainLightRealtimeShadow(TransformWorldToShadowCoord(shadowPoint));
        shadowPoint += rayDir * stepSize;
    }
    return (transmittanceCount / NumShadowPoints);
}

OpticalInfo CumulativeMapInfo(float3 rayOrigin, float3 rayDir, float rayLength){
    float3 densitySamplePoint = rayOrigin; 
    float stepSize = rayLength / (_NumOpticalDepthPoints - 1);
    OpticalInfo info = (OpticalInfo)0;
    for(uint i = 0; i < _NumOpticalDepthPoints; i++){
        OpticalInfo sampleData = SampleMapData(densitySamplePoint);

        info.opticalDensity += sampleData.opticalDensity / (_IsoLevel * 255.0f);
        info.scatterCoeffs += sampleData.scatterCoeffs;
        info.extinctionCoeff += sampleData.extinctionCoeff;

        densitySamplePoint += rayDir * stepSize;
    }
    info.opticalDensity /= _NumOpticalDepthPoints;
    info.scatterCoeffs /= _NumOpticalDepthPoints;
    info.extinctionCoeff /= _NumOpticalDepthPoints;
    info.scatterCoeffs *= calculateOcclusionFactor(rayOrigin, rayDir, rayLength);
    
    return info;
}

float3 opticalDepth(float3 rayOrigin, float3 rayDir, float rayLength){
    float3 densitySamplePoint = rayOrigin;
    float stepSize = rayLength / (_NumOpticalDepthPoints - 1);
    float3 opticalDepth = 0;

    for(uint i = 0; i < _NumOpticalDepthPoints; i++){
        OpticalDepth mapData = SampleOpticalDepth(densitySamplePoint);
        opticalDepth += mapData.scatterCoeffs * (mapData.opticalDensity / (_IsoLevel * 255.0f)) * stepSize;
        densitySamplePoint += rayDir * stepSize;
    }

    return opticalDepth;
}

void ConstructSampleTree(ScatterData cNode, uint pixelIndex, uint depth){
    depth += _NumInScatterPoints; uint parent = depth >> 1;
    uint numTreeLocks = ceil(_NumInScatterPoints / 32.0f);
    uint lockHandle = 0xFFFFFFFF;

    //Parallel Binary Index Tree Construction
    while(depth != 0){//>> 5 means / 32
        uint lockIndex = pixelIndex * numTreeLocks + (parent >> 5);
        uint texelIndex = pixelIndex * _NumInScatterPoints * 2 + depth;
        mapData[texelIndex] = cNode;

        InterlockedXor(treeLocks[lockIndex], 1 << (parent & 0x1F), lockHandle);
        if((lockHandle >> (parent & 0x1F)) & 0x1 == 0) return; 
        //If lock bit is 0, its sibling is second and has not completed yet
        //If lock bit is 1, its sibling is first, so we can safely access it 

        ScatterData sibling = mapData[texelIndex ^ 0x1];
        cNode.inScatteredLight += sibling.inScatteredLight;
        cNode.extinction += sibling.extinction;
        depth >>= 1; parent >>= 1;
    }
}


const static uint numThreads = 8;
[numthreads(numThreads,numThreads,numThreads)]
void Bake (uint3 id : SV_DispatchThreadID)
{
    uint viewX = id.x;
    uint viewY = id.y;
    uint depth = id.z;

    if(viewX >= screenWidth || viewY >= screenHeight || depth >= _NumInScatterPoints)
        return;
    
    uint pixelIndex = indexFromCoord2DManual(viewX, viewY, screenHeight);
    float3 direction = rayInfo[pixelIndex];

    float stepSize = _AtmosphereRadius / (_NumInScatterPoints - 1);
    float3 inScatterPoint = _WorldSpaceCameraPos + direction * stepSize * depth;
    float sunRayLength = raySphere(_WorldSpaceCameraPos, _AtmosphereRadius, inScatterPoint, _LightDirection).y; 

    ScatterData scatterData = (ScatterData)0;
    OpticalInfo opticalInfo = CumulativeMapInfo(inScatterPoint, direction, stepSize); 

    //Calculate Scatter Data
    float3 sunDepth = opticalDepth(inScatterPoint, _LightDirection, sunRayLength);
    float3 cameraDepth = opticalDepth(inScatterPoint, -direction, stepSize * depth); 
    float3 transmittance =  exp(-(sunDepth + cameraDepth)); // exp(-t(PPc, lambda)-t(PPa, lambda)) 
    scatterData.inScatteredLight = opticalInfo.scatterCoeffs * opticalInfo.opticalDensity * transmittance;
    scatterData.extinction = opticalInfo.extinctionCoeff * opticalInfo.opticalDensity;
   
    ConstructSampleTree(scatterData, pixelIndex, depth);
}
