#include "Assets/Resources/Compute/MapData/CCoordHash.hlsl"
// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CleanChunk
#pragma kernel CopyHash
const static int numThreads = 128;
StructuredBuffer<CInfo> _ChunkAddressDict;
StructuredBuffer<uint2> _DirectAddress;

RWStructuredBuffer<uint> _MemoryBuffer;
RWStructuredBuffer<uint2> DirtySubChunks;
uint chunkLHOffset;
uint chunkLMOffset;
uint nChunkAddress;
uint CopyMap;
//This is the coordinate of the origin
int3 CCoord;


uint bSTART;
uint bCOUNT;
uint QueueSize;

uint subChunksAxis;
uint subChunkSize;
//Number of hash bits divided by 32
uint numLightUnits; 

bool IsRealChunk(in CInfo cHandle){ return cHandle.address != 0 && (cHandle.offset & 0xFF) == 1; }
bool IsHashOrigin(in CInfo cHandle, in uint CIndex){ return cHandle.address != 0 && CIndex == HashCoord(cHandle.CCoord); }

[numthreads(numThreads,1,1)]
void CleanChunk (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= numLightUnits) return;
    
    uint rLight = 0;
    uint CIndex = HashCoord(CCoord);
    CInfo cHandle = _ChunkAddressDict[HashCoord(CCoord)];
    if(IsRealChunk(cHandle) && CopyMap){
        uint rAddress = cHandle.address + chunkLMOffset + id.x;
        rLight = _MemoryBuffer[rAddress];
    }
    uint wAddress = _DirectAddress[nChunkAddress].y;
    wAddress += chunkLMOffset + id.x;
    _MemoryBuffer[wAddress] = rLight;
}

const static int numThreadsS = 32;
[numthreads(numThreadsS,1,1)]
void CopyHash (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= numLightUnits) return;

    uint bitMap = 0;

    uint CIndex = HashCoord(CCoord);
    CInfo cHandle = _ChunkAddressDict[CIndex];
    if(IsHashOrigin(cHandle, CIndex)){
        uint rAddress = cHandle.address + chunkLHOffset + id.x;
        bitMap = _MemoryBuffer[rAddress];
    }

    [unroll]for(int i = 0; i < 32; i++){
        uint bit = (bitMap >> i) & 0x1;
        if(bit == 0x1) continue;
        uint SIndex = id.x * 32 + i;
        uint ind;
        InterlockedAdd(DirtySubChunks[bCOUNT].y, 1, ind);
        ind = bSTART + ((ind + DirtySubChunks[bCOUNT].x) % QueueSize);
        DirtySubChunks[ind] = uint2(CIndex, SIndex);
    } 

    uint wAddress = nChunkAddress + chunkLHOffset + id.x;
    _MemoryBuffer[wAddress] = 0xFFFFFFFF; 
}