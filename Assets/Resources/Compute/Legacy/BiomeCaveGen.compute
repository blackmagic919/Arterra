// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#include "Assets/Resources/Compute/Utility/PerlinNoiseSampler.hlsl"
#include "Assets/Resources/Compute/Utility/GetIndex.hlsl"
const static uint SURF_STRIDE_4BYTE = 4;

struct CaveGen{
    float coarse;
    float shape;
    float frequency;
};

StructuredBuffer<CaveGen> _BiomeCaveData;
uint coarseSampler;
uint fineSampler;

StructuredBuffer<uint> _SurfMemoryBuffer;
StructuredBuffer<uint> _SurfAddressDict;
uint surfAddress;
uint meshSkipInc;
uint chunkSize;

RWStructuredBuffer<float> densityMap;

float GetNoiseCentered(float val, float center){
    float clamped = clamp(val, 0, 1);
    float ret = (clamped > center) ? 1-smoothstep(center, 1, clamped)
                : smoothstep(0, center, clamped);
                
    return ret;
}

int GetBiome(uint2 coord){
    uint2 fullCoord = coord * meshSkipInc;
    uint numPointsAxisFull = (numPointsPerAxis - 1) * meshSkipInc + 1;
    uint address = _SurfAddressDict[surfAddress] + indexFromCoord2DManual(fullCoord, numPointsAxisFull) * SURF_STRIDE_4BYTE;
    return asint(_SurfMemoryBuffer[address + 3]);
}

const static int numThreads = 8;
[numthreads(numThreads,numThreads,numThreads)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numPointsPerAxis || id.y >= numPointsPerAxis || id.z >= numPointsPerAxis ) {
        return;
    }

    uint index = indexFromCoord(id);
    uint mapIndex = indexFromCoord2D(id.xz);

    int biome = GetBiome(id.xz);
    CaveGen caveData = _BiomeCaveData[biome];

    float coarseNoise = GetNoise(id, coarseSampler);
    float fineNoise = GetNoise(id, fineSampler);

    float coarseCentered = GetNoiseCentered(coarseNoise, caveData.shape);
    float fineCentered = GetNoiseCentered(fineNoise, caveData.shape);

    float centeredNoise = caveData.coarse * coarseCentered + (1.0f - caveData.coarse) * fineCentered;
    float baseDensity = pow(abs(1.0f-centeredNoise), caveData.frequency); //We do 1-noise because cave means low density

    densityMap[index] = baseDensity;
}
