// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#include "Assets/Resources/TerrainGeneration/SurfaceChunk/BiomeSampler.hlsl"
#include "Assets/Resources/Utility/PerlinNoiseSampler.hlsl"
#include "Assets/Resources/Utility/GetIndex.hlsl"
const static float precisionOffset = 1000.0f;

uint continentalSampler;
uint PVSampler;
uint erosionSampler;
uint squashSampler;
uint caveFreqSampler;
uint caveSizeSampler;
uint caveShapeSampler;

float maxTerrainHeight;
float squashHeight;

float heightOffset;


struct surfData{
    float terrain;
    float squash;
    int biome;
    float cFreq;
    float cSize;
    float cShape;
};

RWStructuredBuffer<surfData> surfMap;

const static int numThreads = 8;

[numthreads(numThreads,numThreads,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numPointsPerAxis || id.y >= numPointsPerAxis) {
        return;
    }

    uint index = indexFromCoord2D(id.x, id.y);

    int3 pos3D = int3(id.x, 0, id.y);

    float rawMaps[6];
    rawMaps[1] = GetRawNoise(pos3D, erosionSampler);
    rawMaps[2] = GetRawNoise(pos3D, squashSampler);
    rawMaps[3] = GetRawNoise(pos3D, PVSampler);
    rawMaps[4] = GetRawNoise(pos3D, continentalSampler);

    float PVNoise = interpolateValue(rawMaps[3], PVSampler);
    float continentalNoise = interpolateValue(rawMaps[4], continentalSampler);
    float erosionNoise = interpolateValue(rawMaps[1], erosionSampler);
    float squashNoise = interpolateValue(rawMaps[2], squashSampler) * precisionOffset;

    surfData data;
    rawMaps[0] = continentalNoise + PVNoise * erosionNoise;
    data.terrain = rawMaps[0] * maxTerrainHeight + heightOffset;
    data.squash = squashNoise * (squashHeight / precisionOffset);

    rawMaps[3] = GetRawNoise(pos3D, caveFreqSampler);
    rawMaps[4] = GetRawNoise(pos3D, caveSizeSampler);
    rawMaps[5] = GetRawNoise(pos3D, caveShapeSampler);
    
    data.biome = GetBiome(rawMaps);
    data.cFreq = interpolateValue(rawMaps[3], caveFreqSampler);
    data.cSize = interpolateValue(rawMaps[4], caveSizeSampler);
    data.cShape = interpolateValue(rawMaps[5], caveShapeSampler);

    surfMap[index] = data;
}
